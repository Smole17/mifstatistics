plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "fabric-loom" version "1.6-SNAPSHOT"
    id "maven-publish"
}

version = project.mod_version
group = project.maven_group
base.archivesName = project.archives_base_name

configurations {
    shadow
}

repositories {
    mavenLocal()
    maven { url = "https://maven.nucleoid.xyz/" }
    maven { url = "https://maven.ladysnake.org/releases" }
    maven { url = "https://api.modrinth.com/maven" }
    maven { url = "https://jitpack.io" }
    maven { url = "https://maven.wispforest.io/" }
    maven { url = "https://maven.shedaniel.me" }
    maven {
        url = "https://maven.saps.dev/releases"
        content {
            includeGroup "dev.ftb.mods"
            includeGroup "dev.latvian.mods"
        }
    }
    maven {
        url = "https://maven.architectury.dev"
        content {
            includeGroup "dev.architectury"
        }
    }
}

dependencies {
    compileOnly "org.projectlombok:lombok:1.18.28"
    annotationProcessor "org.projectlombok:lombok:1.18.28"

    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

    shadow "io.prometheus:prometheus-metrics-core:${project.prometheus_version}"
    shadow "io.prometheus:prometheus-metrics-exporter-httpserver:${project.prometheus_version}"

    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    annotationProcessor modImplementation("io.wispforest:owo-lib:${project.owo_version}")
    modImplementation "maven.modrinth:numismatic-overhaul:${project.numismatic_overhaul_modrinth_version}"

    modImplementation include("dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cca_version}")
    modImplementation include("dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${project.cca_version}")
    modImplementation include("eu.pb4:placeholder-api:${project.placeholder_api}")
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    inputs.property "owo_version", project.owo_version
    inputs.property "numismatic_overhaul_version", project.numismatic_overhaul_version
    inputs.property "numismatic_overhaul_modrinth_version", project.numismatic_overhaul_modrinth_version
    inputs.property "cca_version", project.cca_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version,
                "owo_version": project.owo_version,
                "numismatic_overhaul_version": project.numismatic_overhaul_version,
                "numismatic_overhaul_modrinth_version": project.numismatic_overhaul_modrinth_version,
                "cca_version": project.cca_version
    }
}

java {
    withSourcesJar()
}

shadowJar {
    configurations = [project.configurations.shadow]
    exclude("META-INF")
}

remapJar {
    archiveBaseName = base.archivesName
    dependsOn(shadowJar)
    mustRunAfter(shadowJar)
    inputFile = file(shadowJar.archivePath)
}

tasks.withType(JavaCompile)*.options*.encoding = "UTF-8"